Процедура ОбработкаПроведения(Отказ,Режим)
	//ОбработкаПроведения_НоваяМетодика(Отказ, Режим);
	ОбработкаПроведения_СтараяМетодика(Отказ, Режим);
КонецПроцедуры


Процедура ОбработкаПроведения_НоваяМетодика(Отказ, Режим)
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасходныйКассовыйОрдер.Счет,
		|	РасходныйКассовыйОрдер.Сумма
		|ПОМЕСТИТЬ ВТ_ДанныеДокумента
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
		|ГДЕ
		|	РасходныйКассовыйОрдер.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеДокумента.Счет,
		|	ВТ_ДанныеДокумента.Сумма
		|ИЗ
		|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Движения.Касса.Записывать = Истина;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.Касса.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.СчетОрганизации = Счет;
		Движение.Сумма = Сумма;
	КонецЦикла;
	Движения.Касса.Записать();


	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КассаОстатки.СуммаОстаток КАК СуммаОстаток,
		|	КассаОстатки.СчетОрганизации.НомерСчета КАК СчетОрганизацииНомерСчета
		|ИЗ
		|	РегистрНакопления.Касса.Остатки(&Дата, СчетОрганизации В
		|		(ВЫБРАТЬ
		|			ВТ_ДанныеДокумента.Счет
		|		ИЗ
		|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК КассаОстатки
		|ГДЕ
		|	КассаОстатки.СуммаОстаток < 0";
		
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		ГраницаОстатков = '00010101';
	Иначе 
		ГраницаОстатков = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата", ГраницаОстатков);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() = Ложь Тогда
		Отказ = Истина;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "На счете " + ВыборкаДетальныеЗаписи.СчетОрганизацииНомерСчета + 
			" недостаточно средств в сумме " + (-ВыборкаДетальныеЗаписи.СуммаОстаток) + " руб.";
			Сообщение.Сообщить();
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры


Процедура ОбработкаПроведения_СтараяМетодика(Отказ, Режим)
	Движения.Касса.Записывать = Истина;
	Движения.Касса.Очистить();
	Движения.Касса.Записать();
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасходныйКассовыйОрдер.Сумма,
		|	РасходныйКассовыйОрдер.Счет
		|ПОМЕСТИТЬ ВТ_ДанныеДокумента
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
		|ГДЕ
		|	РасходныйКассовыйОрдер.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КассаОстатки.СчетОрганизации,
		|	КассаОстатки.СуммаОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.Касса.Остатки(&Период, СчетОрганизации В
		|		(ВЫБРАТЬ
		|			ВТ_ДанныеДокумента.Счет
		|		ИЗ
		|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК КассаОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.СчетОрганизации КАК СчетОрганизации,
		|	ВТ_Остатки.СуммаОстаток КАК СуммаОстаток,
		|	ВТ_ДанныеДокумента.Сумма КАК Сумма,
		|	ВТ_ДанныеДокумента.Счет КАК Счет
		|ИЗ
		|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
		|		ПО ВТ_ДанныеДокумента.Счет = ВТ_Остатки.СчетОрганизации";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", МоментВремени());
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Касса");
	ЭлементБлокировки.УстановитьЗначение("СчетОрганизации", Ссылка.Счет);
	Блокировка.Заблокировать();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Дефицит = ВыборкаДетальныеЗаписи.Сумма - ВыборкаДетальныеЗаписи.СуммаОстаток;
		Если Дефицит > 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "На счете " + ВыборкаДетальныеЗаписи.Счет + 
			" недостаточно средств в сумме " + Дефицит + " руб.";
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.Касса.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.СчетОрганизации = Счет;
		Движение.Сумма = Сумма;
		
	КонецЦикла;
	Движения.Касса.Записывать = Истина;

КонецПроцедуры


