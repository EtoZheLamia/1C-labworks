Процедура ОбработкаПроведения(Отказ,Режим)
	ОбработкаПроведенияОстаткиЭлектросамокатов_НоваяМетодика(Отказ, Режим);
	ОбработкаПроведенияАрендаЭлектросамоката(Отказ, Режим);
КонецПроцедуры

Процедура ОбработкаПроведенияОстаткиЭлектросамокатов_НоваяМетодика(Отказ, Режим)
	МенеджерВТ = Новый МенеджерВременныхТаблиц();	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СдачаВАренду.Электросамокат КАК Электросамокат,
		|	СдачаВАренду.Студент КАК Студент
		|ПОМЕСТИТЬ ВТ_СамокатДокумента
		|ИЗ
		|	Документ.СдачаВАренду КАК СдачаВАренду
		|ГДЕ
		|	СдачаВАренду.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СамокатДокумента.Электросамокат КАК Электросамокат,
		|	ВТ_СамокатДокумента.Студент КАК Студент
		|ИЗ
		|	ВТ_СамокатДокумента КАК ВТ_СамокатДокумента";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Движения.ОстаткиЭлектросамокатов.Записывать = Истина;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		// регистр ОстаткиЭлектросамокатов
		Движение = Движения.ОстаткиЭлектросамокатов.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Электросамокат = ВыборкаДетальныеЗаписи.Электросамокат;
		Движение.Количество = 1;
		
	КонецЦикла;
	
	Движения.ОстаткиЭлектросамокатов.Записать();  
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОстаткиЭлектросамокатовОстатки.Электросамокат КАК Электросамокат,
		|	ОстаткиЭлектросамокатовОстатки.КоличествоОстаток КАК Количество,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ОстаткиЭлектросамокатовОстатки.Электросамокат) КАК НоменклатураПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиЭлектросамокатов.Остатки(&Дата, Электросамокат В
		|		(ВЫБРАТЬ
		|			ВТ_СамокатДокумента.Электросамокат КАК ВТ_Электросамокат
		|		ИЗ
		|			ВТ_СамокатДокумента КАК ВТ_СамокатДокумента)) КАК ОстаткиЭлектросамокатовОстатки
		|ГДЕ
		|	ОстаткиЭлектросамокатовОстатки.КоличествоОстаток < 0";

	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		ГраницаОстатков = '00010101'
	Иначе
		ГраницаОстатков = Новый Граница(МоментВремени(),ВидГраницы.Включая);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата", ГраницаОстатков);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Электросамокат " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + " отсутствует на складе.";
			Сообщение.Сообщить();	
		КонецЦикла;	
	КонецЕсли;

КонецПроцедуры


Процедура ОбработкаПроведенияАрендаЭлектросамоката(Отказ, Режим)
	
	Движения.АрендаЭлектросамоката.Записывать = Истина;
	Движения.АрендаЭлектросамоката.Очистить();
	Движения.АрендаЭлектросамоката.Записать();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СдачаВАренду.Студент КАК Студент,
		|	СдачаВАренду.Электросамокат КАК Электросамокат
		|ПОМЕСТИТЬ ВТ_ДанныеДокумента
		|ИЗ
		|	Документ.СдачаВАренду КАК СдачаВАренду
		|ГДЕ
		|	СдачаВАренду.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АрендаЭлектросамокатаОстатки.Студент КАК Студент,
		|	АрендаЭлектросамокатаОстатки.Электросамокат КАК Электросамокат,
		|	АрендаЭлектросамокатаОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.АрендаЭлектросамоката.Остатки(&Дата, Студент В
		|		(ВЫБРАТЬ
		|			ВТ_ДанныеДокумента.Студент
		|		ИЗ
		|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК АрендаЭлектросамокатаОстатки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Студент " + ВыборкаДетальныеЗаписи.Студент + " уже арендовал электросамокат - " + ВыборкаДетальныеЗаписи.Электросамокат;
		Сообщение.Сообщить();
			
	Иначе
		
		Движения.АрендаЭлектросамоката.Записывать = Истина;
		Движение = Движения.АрендаЭлектросамоката.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Студент = Студент;
		Движение.Электросамокат = Электросамокат;
		Движение.Количество = 1;
		Движения.АрендаЭлектросамоката.Записать();	
			
	КонецЕсли;
	
КонецПроцедуры

	
	












